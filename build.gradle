apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply from: 'http://tellurianring.com/projects/gradle-plugins/gradle-release/apply.groovy'

group = 'org.sejda'
description = "Package containing tasks implemented using iText 5.x"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.5
targetCompatibility = 1.5


repositories {
    mavenLocal()
    mavenCentral()
    maven {  url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile group: 'org.sejda', name: 'sejda-model', version: '1.0.0.M11-SNAPSHOT'
    compile group: 'org.sejda', name: 'sejda-core', version: '1.0.0.M11-SNAPSHOT'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.7'
    compile group: 'com.itextpdf', name: 'itextpdf', version:'5.5.5'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version:'1.49'
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version:'1.49'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.2'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.10.8'
    testCompile group: 'org.hibernate', name: 'hibernate-validator', version:'4.2.0.Final'
    testCompile group: 'jdepend', name: 'jdepend', version:'2.9.1'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

release {
    tagPrefix = 'v'
    failOnPublishNeeded = false
    failOnCommitNeeded = false
}

signing {
    required { isReleaseVersion }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'sejda-itext5'
                packaging 'jar'
                description 'Sejda tasks implementation using iText 5.x'
                url 'https://github.com/torakiki/sejda-itext5/'

                scm {
                    url 'scm:git@github.com:torakiki/sejda-itext5.git'
                    connection 'scm:git@github.com:torakiki/sejda-itext5.git'
                    developerConnection 'scm:git@github.com:torakiki/sejda-itext5.git'
                }

                licenses {
                    license {
                        name 'GNU Afferp General Public License (AGPL), Version 3.0'
                        url 'http://www.gnu.org/licenses/agpl-3.0.html'
                        distribution 'repo'
                        comments 'AGPLv3'
                    }
                }

                developers {
                    developer {
                        id 'torakiki'
                        name 'Andrea Vacondio'
                        email 'andrea.vacondio@gmail.com'
                    }
                }
            }
        }
    }
}

